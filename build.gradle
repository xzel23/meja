buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "gradle.plugin.com.github.spotbugs:gradlePlugin:1.6.0"
  }
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'com.github.spotbugs'
apply plugin: 'jacoco'
apply plugin: 'eclipse'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'com.dua3.meja'

// make JavaFX classes accessible in eclipse
import org.gradle.plugins.ide.eclipse.model.AccessRule
eclipse {
    classpath {
        file {
            whenMerged {
                def jre = entries.find { it.path.contains 'org.eclipse.jdt.launching.JRE_CONTAINER' }
                jre.accessRules.add(new AccessRule('0', 'javafx/**'))
            }
        }
    }
}

repositories {
    mavenLocal()
    jcenter()
    // integration for dependencies on github
    maven { url 'https://jitpack.io' }
}

sourceSets {
	main {
		java.srcDirs 'src/main/java'
	}

	swing {
		java.srcDirs 'src/swing/java'
	}
	
	javafx {
		java.srcDirs 'src/javafx/java'
	}
	
	samples {
		java.srcDirs 'src/samples/java'
	}
}

dependencies {
    def poiVersion = "3.17"
    compile "org.apache.poi:poi:${poiVersion}"
    compile "org.apache.poi:poi-ooxml:${poiVersion}"
    compile "org.apache.poi:poi-ooxml-schemas:${poiVersion}"

    // use local project if 'localBuild' was set
    def utilityVersion = "0.5"
    if (project.hasProperty('localBuild')) {
        compile 'com.dua3.utility:utility:+'
        swingCompile 'com.dua3.utility:utility:+'
        javafxCompile 'com.dua3.utility:utility:+'
        samplesCompile 'com.dua3.utility:utility:+'
    } else {
        compile "com.github.xzel23:utility:${utilityVersion}"
        swingCompile "com.github.xzel23:utility:${utilityVersion}"
        javafxCompile "com.github.xzel23:utility:${utilityVersion}"
        samplesCompile "com.github.xzel23:utility:${utilityVersion}"
    }
	
	// https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    def sl4jVersion = "1.7.25"
	compile "org.slf4j:slf4j-api:${sl4jVersion}"
	testRuntime "org.slf4j:slf4j-simple:${sl4jVersion}"
	samplesRuntime "org.slf4j:slf4j-simple:${sl4jVersion}"
	
    testCompile "junit:junit:4.12"

  	swingCompile sourceSets.main.output
  	javafxCompile sourceSets.main.output
  	samplesCompile sourceSets.main.output, sourceSets.swing.output, sourceSets.javafx.output
}

configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor 0, 'seconds'
    }
}

spotbugs {
    excludeFilter = file("$rootProject.projectDir/config/spotbugs-exclude.xml")
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

tasks.withType(JacocoReport) {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
}

// create a jar with the dependencies
task depJar(type: Jar, dependsOn: tasks['classes']) {
    baseName = 'meja'
    classifier = 'dependencies'
    
    from rootProject.configurations.compile.collect { 
        it.isDirectory() ? it : zipTree(it) 
    }
}

// create a jar with sources
task srcJar(type: Jar, dependsOn: tasks['classes']) {
    baseName = 'meja'
    classifier = 'sources'
    
    from rootProject.sourceSets.main.java.srcDirs
        
    subprojects.each { project ->
        from project.sourceSets.main.java.srcDirs
    }
}

// create a jar with javadoc
task docJar(type: Jar, dependsOn: javadoc) {
    baseName = 'meja'    
    classifier = 'javadoc'

    from file("$buildDir/docs/javadoc")    
}

// collect all javadoc documentation in one location
task alljavadoc(type: Javadoc, description: 'Generate javadocs from all child projects as if it was a single project', group: 'Documentation') {
    destinationDir = file("$buildDir/docs/javadoc")
    title = "$project.name $version API"
    options.author true
    
    allprojects.each { proj ->
        proj.tasks.withType(Javadoc).each { javadocTask ->
            source += javadocTask.source
            classpath += javadocTask.classpath
            excludes += javadocTask.excludes
            includes += javadocTask.includes
        }
    }
}

// create a jar with classes from all subprojects
task allJar(type: Jar, dependsOn: [tasks['classes'], alljavadoc]) {
    baseName = 'meja'
    classifier = 'all'
    
    from sourceSets.main.output
    from sourceSets.swing.output
    from sourceSets.javafx.output
    
    // javadoc
    from file("$buildDir/docs/javadoc")    
}

task dist(dependsOn: [depJar, srcJar, docJar, allJar]) {
    println "creating distribution files"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives allJar
    archives sourcesJar
    archives javadocJar
    archives depJar
}

defaultTasks 'build', 'publishToMavenLocal', 'install'
