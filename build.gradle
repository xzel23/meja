plugins {
  id "com.github.spotbugs" version "1.6.0"
  id "com.jfrog.bintray" version "1.8.0"
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java-library'
apply plugin: 'com.github.spotbugs'

/////////////////////////////////////////////////////////////////////////////
    group              = 'com.dua3.meja'
    version            = '1.0-beta13'
    
    ext.moduleName     = project.group 
    ext.scm            = 'https://github.com/xzel23/meja.git'
    ext.repo           = 'public'
    ext.description    = 'meja spreadsheet library'
    
    ext.developerId    = 'xzel23'
    ext.developerName  = 'Axel Howind'
    ext.developerEmail = 'axel@dua3.com'

    sourceSets {
        main    { java.srcDirs 'src/main/java'    }
        
        swing   { java.srcDirs 'src/swing/java'   }
        
        samples { java.srcDirs 'src/samples/java' }
    }
    
/////////////////////////////////////////////////////////////////////////////

repositories {
    mavenLocal()
    jcenter()
    maven { url  "https://dl.bintray.com/dua3/public" }
}

dependencies {
    // Apache POI
    def poiVersion = "3.17"
    compile "org.apache.poi:poi:${poiVersion}"
    compile "org.apache.poi:poi-ooxml:${poiVersion}"
    compile "org.apache.poi:poi-ooxml-schemas:${poiVersion}"

    // dua3 utility
    compile        group: 'com.dua3.utility', name: 'utility', version: '0.11.2'
    swingCompile   group: 'com.dua3.utility', name: 'utility', version: '0.11.2'
    samplesCompile group: 'com.dua3.utility', name: 'utility', version: '0.11.2'
    
    // Log4j2
    compile        group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.10.0'
    testRuntime    group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.10.0'
    samplesRuntime group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.10.0'

    // JUnit    
    testCompile "junit:junit:4.12"

    swingCompile   sourceSets.main.output
    samplesCompile sourceSets.main.output, sourceSets.swing.output
}

sourceCompatibility = 8
targetCompatibility = 8

// === JIGSAW === >

for (SourceSet sourceSet: sourceSets) {
    if (!sourceSet.allJava.matching { include '**/module-info.java'}.isEmpty()) {
        logger.info "{} has a module-info.java", sourceSet
        
        // check Java version
        if (!JavaVersion.current().isJava9Compatible()) {
            logger.warn('At least Java 9 is needed if a module-info.java file is present. Build will probably fail!')
        }
        
        // if sourceCompatibility is < Java9, exclude module-info.java from the
        // main sourceSet and compile it seperately with compatibility set to Java9
        if (!sourceCompatibility.isJava9Compatible()) {
            // define task names
            def compileJavaSources = 'compileJavaSources_'+sourceSet.name
            def compileModuleInfo = 'compileModuleInfo_'+sourceSet.name
            
            // define directories
            def name = sourceSet.name
            def sources = sourceSet.java.srcDirs
            def destination = sourceSet.output.classesDirs.singleFile
            def classes = sourceSet.compileClasspath + sourceSet.output
            def modulepath = classes.asPath+";"+destination

            task "$compileJavaSources" (type: JavaCompile) {
                source = sources
                classpath = classes
                exclude '**/module-info.java'
                destinationDir = destination
            }
            
            task "$compileModuleInfo" (type: JavaCompile) {
                doFirst {
                    logger.info "Compiling module-info.java for source set '{}' in Java 9 compatibility mode.", name
                    
                    logger.debug("module-path:\n{}", modulepath.replaceAll(";", "\n"))
                    logger.debug("class-path:\n{}", classpath.asPath.replaceAll(";", "\n"))
    
                    options.compilerArgs = [
                        '--module-path', modulepath,
                        '--add-modules', 'ALL-SYSTEM',
                        '-d', destination
                    ]
                }
                
                inputs.property("moduleName", project.moduleName)
    
                sourceCompatibility = 9
                targetCompatibility = 9
    
                source = sources
                include '**/module-info.java'                
                classpath = classes                 
                destinationDir = destination
                
                dependsOn {
                    "$compileJavaSources"
                }
            }
            
            sourceSet.java { 
                exclude '**/module-info.java'
            }
         
            assemble.dependsOn "$compileModuleInfo"
        }         
    }
}

// === SPOTBUGS === >

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

// === ARTIFACTS === >

for (SourceSet sourceSet: sourceSets) {
    def name = sourceSet.name
    
    def jarName = "jar_$name"
    def sourcesJarName = "sourcesJar_$name"
                
    def suffix = name.equals("main") ? "" : "-$name"
    def base= "meja$suffix" 
        
    task "$jarName"(type: Jar, dependsOn: classes) {
        classifier null
        baseName base
        from sourceSet.output
    }
    
    task "$sourcesJarName"(type: Jar, dependsOn: classes) {
        classifier 'sources'
        baseName base
        from sourceSet.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}    

// === MAVEN publication === >

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
        }
    }
    developers {
        developer {
            id    project.developerId
            name  project.developerName
            email project.developerEmail
        }
    }
    
    scm {
       url project.scm
    }
}

// Create the publication with the pom configuration:
publishing {
    publications {
        for (SourceSet sourceSet: sourceSets) {
            def name = sourceSet.name
            
            if (name.equals("test"))
                break
        
            def suffix = name.equals("main") ? "" : "-$name"
            def pubName= "meja$suffix"
             
            "$pubName"(MavenPublication) {
                def jarName = "jar_$name"
                def sourcesJarName = "sourcesJar_$name"
                def javadocJarName = "javadocJar_$name"
                                                    
                artifact tasks."$jarName"      
                artifact tasks."$sourcesJarName"
                
                if (name.equals("main"))  
                    artifact javadocJar       

                groupId project.group
                artifactId pubName
                version version
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', project.description)
                    root.appendNode('name', pubName)
                    root.appendNode('url', project.scm)
                    root.children().last() + pomConfig
                }
            }
        }
    }
}

// === BINTRAY === >

bintray {
    user = findProperty('BINTRAY_USER') ?: 'NONE'
    key = findProperty('BINTRAY_KEY') ?: 'NONE'

    configurations = ['archives']

    dryRun = false //[Default: false] Whether to run this as dry-run, without deploying
    publish = false //[Default: false] Whether version should be auto published after an upload    
    override = false //[Default: false] Whether to override version artifacts already published    

    pkg {
        repo = project.repo
        name = project.group
        userOrg = findProperty('BINTRAY_USER_ORG') ?: 'NONE'
        licenses = ['Apache-2.0']
        vcsUrl = project.scm

        version {
            name = project.version
            desc = project.name+version
            released  = new Date()
            vcsTag = project.version
        }
    }

    // publish everything    
    def publicationList = []
    for (SourceSet sourceSet: sourceSets) {
        def name = sourceSet.name
        
        if (name.equals("test"))
            break
    
        def suffix = name.equals("main") ? "" : "-$name"
        def pubName= "meja$suffix"
         
        publicationList.add("$pubName")
    }
    publications = publicationList
}

// === DEFAULT TASKS === >

defaultTasks 'build', 'publishToMavenLocal', 'install'
