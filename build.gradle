allprojects {
    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'findbugs'
    apply plugin: 'pmd'
    apply plugin: 'jacoco'
    
    group = 'com.dua3.meja'

    repositories {
        mavenLocal()
        jcenter()
        // integration for dependencies on github
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        def poiVersion = "3.16"
        compile "org.apache.poi:poi:${poiVersion}"
        compile "org.apache.poi:poi-ooxml:${poiVersion}"
        compile "org.apache.poi:poi-ooxml-schemas:${poiVersion}"

        // use local project if 'localBuild' was set
        def utilityVersion = "0.1.5"
        if (project.hasProperty('localBuild')) {
            compile 'com.dua3.utility:utility:+'
        } else {
            compile "com.github.xzel23:utility:${utilityVersion}"
        }
		
        testCompile "junit:junit:4.12"
    }

    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled false
            html.enabled true
        }

        findbugs {
            effort = "max"
            reportLevel = "high"
        }
    }

    tasks.withType(JacocoReport) {
        reports {
            xml.enabled false
            csv.enabled false
            html.enabled true
        }
    }
}

subprojects {
    dependencies {
        compile rootProject
    }    
}

project(':meja-ui-jfx') {
    dependencies {
        compile project(':meja-ui')
    }
}

project(':meja-ui-swing') {
    dependencies {
        compile project(':meja-ui')
    }
}

project(':meja-samples') {
    dependencies {
        compile project(':meja-ui-swing')
        compile project(':meja-ui-jfx')
    }
}
